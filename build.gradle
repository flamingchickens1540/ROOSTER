import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

plugins {
    id "java"
    id "idea"
    id "maven"
    id "jaci.openrio.gradle.GradleRIO" version "2018-beta-5b"
}

group = 'org.team1540'

// for JitPack
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    maven { url "http://first.wpi.edu/FRC/roborio/maven/release" }
    maven { url "http://dev.imjac.in/maven/" }
    maven { url "https://raw.githubusercontent.com/Open-RIO/Maven-Mirror/master/m2" }
}

dependencies {
    compile 'edu.wpi.first.wpilibj:athena:2017.2.1'
    compile 'edu.wpi.first.wpilib.networktables.java:NetworkTables:3.1.7'
    compile 'org.opencv:opencv-java:3.1.0'
    compile 'edu.wpi.cscore.java:cscore:1.0.1'
    compile 'openrio.mirror.third.kauailabs:navx-java:3.0.342'
    compile 'openrio.mirror.third.ctre:CTRE-phoenix-java:5.1.2.1'
}

def TEAM = 1540
def ROBOT_CLASS = "org.team1540.base.testing.XXX"

deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
            /*
            IMPORTANT: With this line in the buildscript, the robot code will not start until
            you connect your debugger to the robot. Connect by running the "Remote Robot Debug"
            run configuration which should be shared through version control. Alternatively,
            remove this line and redeploy.
            */
            debug = true
        }
    }
}

jar {
    println "Using JitPack: " + JITPACK.toString()
    if (!JITPACK) {
        // Since we're deploying to the RIO, create a "fat jar"
        from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
    } // otherwise, default behavior
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}